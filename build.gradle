import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.rosewood'
version = '1.1.19'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://erethon.de/repo/' }
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url = 'https://libraries.minecraft.net' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.lokka30:treasury-api:1.1.1'
    compileOnly 'org.black_ixx:playerpoints:3.1.0'
    compileOnly 'me.realized:tokenmanager:3.2.5'
    compileOnly 'io.lumine:MythicLib-dist:1.3.1'
    compileOnly 'net.Indyuce:MMOItems:6.7.3'
    compileOnly 'net.Indyuce:MMOCore:1.9.3'
    compileOnly 'com.github.jojodmo:ItemBridge:master-SNAPSHOT'
    compileOnly 'com.ssomar:score:3.9.72'
    compileOnly 'com.github.LoneDev6:api-itemsadder:2.5.5'
    compileOnly 'de.erethon:caliburn:1.0.3'
    compileOnly 'com.github.oraxen:oraxen:1.152.2'
    compileOnly 'nl.knokko:customitems:9.18'
    compileOnly 'com.github.emanondev:ItemEdit:2.18'
    compileOnly 'thirtyvirus.uber:uberitems:2.6'
    compileOnly 'com.github.Slimefun:Slimefun4:RC-30'
    compileOnly 'io.lumine:Mythic-Dist:5.1.0-SNAPSHOT'
    compileOnly 'com.willfp:eco:6.24.0'
    compileOnly 'me.wolfyscript:customcrafting:3.16.6.2'
    compileOnly 'com.wolfyscript:utilities:4.16.4.1'
    compileOnly 'dev.rosewood:rosestacker:1.5.7'
    compileOnly 'me.casperge:realisticseasons:8.1.3'
    compileOnly 'com.dfsek.terra:api:6.1.2-BETA+9abac34b8'
    compileOnly 'com.dfsek.terra:common:6.1.2-BETA+9abac34b8'
    compileOnly 'com.dfsek.terra:bukkit:6.1.2-BETA+9abac34b8'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.10.0'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1', {
        exclude group: 'org.spigotmc'
    }

    api 'dev.rosewood:rosegarden:1.2.4'
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseloot.lib.rosegarden')

    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseloot'
            pom {
                name = 'roseloot'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
