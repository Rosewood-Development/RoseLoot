plugins {
    id 'com.gradleup.shadow' version '8.3.5' apply false
    id 'maven-publish'
    id 'java'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.gradleup.shadow'
    group = 'dev.rosewood'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
        disableAutoTargetJvm()
    }

    compileJava {
        options.compilerArgs += ['-parameters']
        options.fork = true
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public/' }
        maven { url = 'https://maven.devs.beer/' }
        maven { url = 'https://repo.luminescent.dev/repository/public-releases/' }
        maven { url = 'https://repo.auxilor.io/repository/maven-public/' }
        maven { url = 'https://erethon.de/repo/' }
        maven { url = 'https://maven.playpro.com/' }
        maven { url = 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
        maven { url = 'https://r.irepo.space/maven/' }
        maven { url = 'https://repo.momirealms.net/releases/' }
        maven { url = 'https://repo.crazycrew.us/releases' }
        maven { url = 'https://repo.groupez.dev/releases' }
        maven { url = 'https://libraries.minecraft.net/' }
        maven { url = 'https://jitpack.io/' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':Component:Latest')
    implementation project(':NMS:Wrapper')
    implementation project(':NMS:v1_16_R3')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'reobf')
    implementation project(path: ':NMS:v1_18_R2', configuration: 'reobf')
    implementation project(path: ':NMS:v1_19_R3', configuration: 'reobf')
    implementation project(path: ':NMS:v1_20_R3', configuration: 'reobf')
    implementation project(path: ':NMS:v1_20_R4', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R1', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R2', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R3', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R4', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R5', configuration: 'reobf')
}

shadowJar {
    archiveClassifier.set('')

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseloot.lib.rosegarden')

    exclude 'com/zaxxer/hikari/**/*.class'
    exclude 'org/slf4j/**/*.class'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseloot'
            pom {
                name = 'roseloot'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username = project.mavenUser
                    password = project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
generateMetadataFileForShadowPublication.dependsOn jar
