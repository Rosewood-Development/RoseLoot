import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.rosewood'
version = '1.2.21-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    disableAutoTargetJvm()
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.luminescent.dev/repository/public-releases/' }
    maven { url = 'https://repo.auxilor.io/repository/maven-public/' }
    maven { url = 'https://erethon.de/repo/' }
    maven { url = 'https://maven.playpro.com/' }
    maven { url = 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    maven { url = 'https://r.irepo.space/maven/' }
    maven { url = 'https://repo.momirealms.net/releases/' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.lokka30:treasury-api:1.1.1'
    compileOnly 'org.black_ixx:playerpoints:3.1.0'
    compileOnly 'me.realized:tokenmanager:3.2.5'
    compileOnly 'su.nightexpress:coinsengine:2.2.0.1'
    compileOnly 'su.nightexpress:nightcore:2.5.1'
    compileOnly 'io.lumine:MythicLib-dist:1.7.1-SNAPSHOT'
    compileOnly 'io.lumine:Mythic-Dist:5.4.0'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT'
    compileOnly 'net.Indyuce:MMOCore-API:1.13.1-SNAPSHOT'
    compileOnly 'com.github.jojodmo:ItemBridge:master-SNAPSHOT'
    compileOnly 'com.ssomar:score:5.24.12.29'
    compileOnly 'com.ssomar:executableitems:7.24.12.29'
    compileOnly 'com.ssomar:executableblocks:5.24.12.29'
    compileOnly 'com.github.LoneDev6:api-itemsadder:2.5.5'
    compileOnly 'de.erethon:caliburn:1.0.3'
    compileOnly 'com.github.oraxen:oraxen:1.161.0'
    compileOnly 'nl.knokko:customitems:9.18'
    compileOnly 'com.github.emanondev:ItemEdit:2.17', {
        exclude group: 'org.spigotmc'
    }
    compileOnly 'thirtyvirus.uber:uberitems:2.6'
    compileOnly 'dev.luminescent:DeezItems:1.1.1@jar'
    compileOnly 'com.github.Slimefun:Slimefun4:RC-37'
    compileOnly 'io.lumine:Mythic-Dist:5.1.0-SNAPSHOT'
    compileOnly 'pers.neige.neigeitems:NeigeItems:1.14.27'
    compileOnly 'com.willfp:eco:6.71.4'
    compileOnly 'com.willfp:EcoSkills:3.58.0'
    compileOnly 'me.wolfyscript:customcrafting:3.16.6.2'
    compileOnly 'com.wolfyscript:utilities:4.16.4.1'
    compileOnly 'dev.rosewood:rosestacker:1.5.24-SNAPSHOT'
    compileOnly 'me.casperge:realisticseasons:8.1.3'
    compileOnly 'com.dfsek.terra:api:6.1.2-BETA+9abac34b8'
    compileOnly 'com.dfsek.terra:common:6.1.2-BETA+9abac34b8'
    compileOnly 'com.dfsek.terra:bukkit:6.1.2-BETA+9abac34b8'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.13.1'
    compileOnly 'net.coreprotect:coreprotect:21.0'
    compileOnly 'net.momirealms:custom-fishing:2.3.3'
    compileOnly 'dev.krakenied:blocktracker:1.0.5'
    compileOnly 'net.advancedplugins:advanceditems:1.9.4'
    compileOnly 'fr.maxlego08:zitems:1.0.0'
    compileOnly 'fr.maxlego08:zessentials:1.0.1.3'
    compileOnly 'com.nexomc:nexo:0.4'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1', {
        exclude group: 'org.spigotmc'
    }

    api 'dev.rosewood:rosegarden:1.4.6'
    api 'dev.rosewood:rosegarden-compatibility:1.4.6'
}

shadowJar {
    archiveClassifier.set('')
    minimize()

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseloot.lib.rosegarden')

    exclude 'com/zaxxer/hikari/**/*.class'
    exclude 'org/slf4j/**/*.class'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = 'roseloot'
            pom {
                name = 'roseloot'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
generateMetadataFileForShadowPublication.dependsOn jar
